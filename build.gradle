plugins {
    id 'java'
    id 'maven-publish'
}

group = "net.mitask"
version = project.hasProperty("override_version") ? (project.properties["override_version"] as String).substring(0, 7) : "1.3.0"

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"

    implementation "com.google.auto.service:auto-service:1.1.1"
    annotationProcessor "com.google.auto.service:auto-service:1.1.1"

    implementation "org.slf4j:slf4j-api:1.7.27"
    implementation "com.google.code.gson:gson:2.11.0"
    implementation "org.yaml:snakeyaml:2.3"

    implementation "com.diogonunes:JColor:5.5.1"
}

publishing {
    repositories {
        maven {
            name = "glassMaven"
            credentials(PasswordCredentials)
            def projVer = project.version as String

            if (projVer.endsWith("-SNAPSHOT") || (projVer.indexOf('.') == -1)) {
                url = "https://maven.glass-launcher.net/snapshots"
            } else {
                url = "https://maven.glass-launcher.net/releases"
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}